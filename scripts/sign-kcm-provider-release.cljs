#!/usr/bin/env nbb
(ns sign-kcm-provider-release
  "Create an Ed25519-signed, content-bound provider release descriptor."
  (:require ["node:fs" :as fs]
            ["node:path" :as path]
            [cljs.reader :as reader]
            [clojure.string :as str]
            [fleet.identity :as identity]
            [fleet.kcm-provider :as provider]))

(def args (vec *command-line-args*))
(defn opt [flag default] (or (second (drop-while #(not= flag %) args)) default))
(defn req [flag] (or (opt flag nil) (throw (ex-info (str "missing " flag) {}))))

(defn canonical [x]
  (cond
    (map? x) (into (sorted-map) (map (fn [[k v]] [k (canonical v)])) x)
    (sequential? x) (mapv canonical x)
    :else x))

(defn canonical-json [x]
  (js/JSON.stringify (clj->js (canonical x))))

(let [archive (path/resolve (req "--archive"))
      manifest-path (path/resolve (req "--manifest"))
      version (req "--version")
      platform (opt "--platform" (str (.-platform js/process) "-" (.-arch js/process)))
      base-url (str/replace (req "--base-url") #"/$" "")
      out (path/resolve (req "--out"))
      signer (identity/resolve-identity (req "--identity"))
      manifest (reader/read-string (fs/readFileSync manifest-path "utf8"))
      _ (provider/verify-manifest! manifest (provider/closure-sha256 manifest))
      archive-name (path/basename archive)
      manifest-name (path/basename manifest-path)
      body {"format" "kotoba-kcm-provider-release/v1"
            "version" version
            "platform" platform
            "provider" {"compilerCid" (str "git:" (:compiler-revision manifest))
                        "moduleLockCid" (str "sha256:" (:module-lock-sha256 manifest))
                        "providerClosureSha256" (provider/closure-sha256 manifest)
                        "runtimeSha256" (provider/file-sha256 js/process.execPath)
                        "files" (count (:files manifest))
                        "bytes" (reduce + (map :size (:files manifest)))}
            "assets" {"archive" {"name" archive-name
                                   "url" (str base-url "/" archive-name)
                                   "sha256" (provider/file-sha256 archive)
                                   "bytes" (.-size (fs/statSync archive))}
                      "manifest" {"name" manifest-name
                                  "url" (str base-url "/" manifest-name)
                                  "sha256" (provider/file-sha256 manifest-path)
                                  "bytes" (.-size (fs/statSync manifest-path))}}
            "signer" (:did signer)}
      signed (assoc body "signature" (identity/sign-hex signer (canonical-json body)))]
  (fs/mkdirSync (path/dirname out) #js {:recursive true})
  (fs/writeFileSync out (str (js/JSON.stringify (clj->js signed) nil 2) "\n"))
  (println (pr-str {:format :kotoba-kcm-provider-release-signed/v1
                    :descriptor out :signer (:did signer)
                    :provider-closure-sha256 (get-in body ["provider" "providerClosureSha256"])})))
