#!/usr/bin/env nbb
(ns kcm-verify
  "Offline verification for a signed KCM pilot EDN receipt."
  (:require ["node:fs" :as fs]
            ["node:path" :as path]
            [cljs.reader :as reader]
            [fleet.kcm-receipt :as receipt]))

(def args (vec *command-line-args*))

(try
  (when (or (empty? args) (some #{"--help" "-h"} args))
    (println "usage: kcm-verify SIGNED-PILOT.edn")
    (js/process.exit (if (empty? args) 64 0)))
  (let [p (path/resolve (first args))
        signed (-> (fs/readFileSync p "utf8") reader/read-string receipt/verify!)]
    (println (pr-str {:format :kotoba-kcm-verification/v1
                      :valid? true
                      :cid (:cid signed)
                      :signer (get-in signed [:signature :signer])
                      :decision (get-in signed [:receipt :decision])
                      :machine-id (get-in signed [:receipt :machine :id])})))
  (catch :default e
    (println (pr-str {:format :kotoba-kcm-verification/v1
                      :valid? false :reason (.-message e)}))
    (set! (.-exitCode js/process) 2)))
