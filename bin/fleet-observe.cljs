#!/usr/bin/env nbb
;; Fleet observer — read the shared coordination log from ANOTHER machine.
;;
;; It holds no fleet key: it mints a CACAO with a throwaway identity of its own
;; and points at the fleet's graph CID explicitly. That is enough to READ (the
;; graph is named, not derived, on the read path) and not enough to WRITE (the
;; edge derives a write's target graph from the writer's own DID), so a machine
;; can watch the fleet without being able to forge a claim.
(ns observe
  (:require ["node:crypto" :as crypto]
            [ed25519.core :as ed]
            [fleet.kotobase-store :as kbs]
            [kotoba.fleet.lease :as lease]
            [kotoba.fleet.store :as store]
            [kotoba.fleet.view :as view]))

(def argv (vec (.-argv js/process)))
(defn- opt [f] (second (drop-while #(not= f %) argv)))

(def seed (js/Uint8Array.
           (js/Buffer.from (.-d (.export (.-privateKey (crypto/generateKeyPairSync "ed25519"))
                                         #js {:format "jwk"}))
                           "base64url")))
(def me {:seed seed :did (ed/did-key-from-seed seed)})

(let [db (kbs/kotobase-store {:identity me
                              :db-name (opt "--db-name")
                              :graph (opt "--graph")})
      ds (store/datoms db)
      now (js/Date.now)]
  (println (pr-str {:observer-host (.trim (.execFileSync (js/require "node:child_process")
                                                         "hostname" #js [] #js {:encoding "utf8"}))
                    :observer-did (:did me)
                    :graph (opt "--graph")
                    :datoms (count ds)
                    :holder (lease/holder db (opt "--unit") now)
                    :view (view/snapshot db now)})))
